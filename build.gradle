
plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'pl.allegro.tech.build.axion-release'

mainClassName = 'org.rundeck.client.tool.App'
defaultTasks 'clean', 'build'
scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}

project.version = scmVersion.version



repositories {
    mavenCentral()
}

dependencies {

    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-jackson:2.0.0-beta4'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
    compile 'com.lexicalscope.jewelcli:jewelcli:0.8.9'

    testCompile group: 'junit', name:'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile "org.codehaus.groovy:groovy-all:2.3.7"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}



task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
