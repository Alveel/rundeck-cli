def bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
def bintrayKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') :
        System.getenv('BINTRAY_API_KEY')
def bintrayUserOrg = project.hasProperty('bintrayUserOrg') ? project.property('bintrayUserOrg') :
        System.getenv('BINTRAY_USER_ORG')
def bintrayRepo = project.hasProperty('bintrayRepo') ? project.property('bintrayRepo') :
        System.getenv('BINTRAY_REPO')
def debUpload = 'true' == (project.hasProperty('debUpload') ? project.property('debUpload') :
        System.getenv('BINTRAY_DEB_UPLOAD'))
//
//def bintrayPassphrase = project.hasProperty('bintray.gpg.password') ? project.property('bintray.gpg.password') :
//        System.getenv('BINTRAY_GPG_PASSWORD')

if (bintrayUser && bintrayKey && bintrayRepo) {
    def includePat = debUpload ? '*.deb' : '*.rpm'
    apply plugin: 'com.jfrog.bintray'
    bintray {
        user = bintrayUser
        key = bintrayKey
        filesSpec {
            from ("$buildDir/distributions"){
                include includePat
            }
            into "."
        }
        dryRun = false
        publish = true
        override = true
        pkg {
            name = 'rundeck-cli'
            repo = bintrayRepo
            if(bintrayUserOrg) {
                userOrg = bintrayUserOrg
            }
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/rundeck/rundeck-cli.git'
            websiteUrl = 'https://github.com/rundeck/rundeck-cli'
            issueTrackerUrl = 'https://github.com/rundeck/rundeck-cli/issues'
            githubRepo = 'rundeck/rundeck-cli' //Optional Github repository
            githubReleaseNotesFile = 'README.md' //Optional Github readme file

            version {
                name = rpmVersion
                desc = "Rundeck CLI Tools ${rpmVersion}"
                vcsTag = "v${rpmVersion}"
            }
        }

    }
}